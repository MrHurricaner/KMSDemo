apply plugin: 'com.android.application'
apply plugin: 'realm-android'

android {

    compileSdkVersion rootProject.ext.android.compileSdkVersion

    defaultConfig {
        applicationId "com.kms.demo"
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionName "${version_name}"
        versionCode Integer.parseInt(version_code)
        buildConfigField("String", "BETA_COUNT", "\"${beta_count}\"")
        buildConfigField("String", "RELEASE_TYPE", "${releaseTypeC}")

    }

    signingConfigs {
        debug {
            storeFile file('../extras/debug.keystore')
        }

        release {
            storeFile file("../extras/wallet.keystore")
            storePassword "123456"
            keyAlias "wallet"
            keyPassword "123456"
        }
    }


    buildTypes {

        debug {
            debuggable true
            signingConfig signingConfigs.debug
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "RELEASE_TYPE", "${releaseTypeC}")
            multiDexEnabled true
        }

        releaseC {
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "RELEASE_TYPE", "${releaseTypeC}")
            multiDexEnabled true
        }

        releaseX {
            debuggable false
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField("String", "RELEASE_TYPE", "${releaseTypeX}")
            zipAlignEnabled true
            multiDexEnabled true
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def verName = android.defaultConfig.versionName
            def verCode = android.defaultConfig.versionCode
            def innerVer = beta_count
            def releaseTime = new Date().format("yyyyMMddHHmm")

            def type = ""
            if (variant.buildType.name == "releaseC") {
                type = "c"
            } else if (variant.buildType.name == "releaseX") {
                type = "x_unjiagu"
            } else if (variant.buildType.name == "debug") {
                type = "debug"
                // 调试若使用实时时间戳会导致apk无法找到，因此，这里固定为0
                releaseTime = "0"
            }
            if (variant.flavorName != "kms") {
                type = (type + "_" + variant.flavorName)
            }

            outputFileName = "kms_android_v${verName}_${innerVer}_${releaseTime}_${verCode}.apk";
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven {
        url "https://jitpack.io"
    }
    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation project(':appcore')
    implementation project(':zxing')
    implementation rootProject.ext.dependencies.core
    implementation rootProject.ext.dependencies.appcompat
    implementation rootProject.ext.dependencies.multidex
    implementation rootProject.ext.dependencies.constraintlayout
    implementation rootProject.ext.dependencies.recyclerview
    implementation rootProject.ext.dependencies.rxjava2_rxjava
    implementation rootProject.ext.dependencies.rxjava2_rxandroid
    implementation rootProject.ext.dependencies.rxlifecycle
    implementation rootProject.ext.dependencies.rxlifecycle_android
    implementation rootProject.ext.dependencies.rxbinding3
    implementation rootProject.ext.dependencies.eventbus
    implementation rootProject.ext.dependencies.permission
    implementation rootProject.ext.dependencies.butterknife
    annotationProcessor rootProject.ext.dependencies.butterknife_compiler
    implementation rootProject.ext.dependencies.libphonenumber
    implementation rootProject.ext.dependencies.svgaPlayer
    implementation rootProject.ext.dependencies.web3j_core
    implementation rootProject.ext.dependencies.web3j_crypto
    implementation rootProject.ext.dependencies.web3j_utils
    implementation(name: 'kms-sdk-android-release', ext: 'aar')
}
